import org.gradle.nativeplatform.platform.OperatingSystem
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.21.1'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.javatuples:javatuples:1.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

javafx {
    version = "14"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing' ]
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

mainClassName = "sample.Main"

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher{
        name = 'sample.FakeMain'
    }

    jpackage {
        outputDir = 'jpackage'
        imageName = 'CAViewer'
        skipInstaller = true
        installerName = 'CAViewer'
    }
}

task generateKeystore() {
    doLast {
        def dName = /CN=CAViewer, OU=CAViewer, O=CAViewer, L=Singapore, S=Singapore, C=SG/
        def keytoolCommand = /keytool -genkeypair -keyalg RSA -keysize 2048 -sigalg SHA1withRSA -dname "$dName" -validity 365 -keystore key.keystore -storepass password2020/
        println keytoolCommand

        def output = keytoolCommand.execute().toString()
        println output
    }
}

task zip() {
    doLast {
        OperatingSystem os = DefaultNativePlatform.currentOperatingSystem;
        if (os.macOsX) {
            def buildFolder = "build/jpackage/CAViewer.app"

            def command = "jar -cfM CAViewer.zip $buildFolder"
            command.execute()
        }
        else {
            def buildFolder = "build/jpackage/CAViewer"

            def command = "jar -cfM CAViewer.zip $buildFolder"
            command.execute()
        }
    }
}